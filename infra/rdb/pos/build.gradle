plugins {
    id 'java-library'
    id 'java-test-fixtures'
}

bootJar { enabled = false }
jar { enabled = true }


dependencies {
    // Spring Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // MySQL / H2
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // domain 모듈
    compileOnly project(':domain:domain-pos')

    // common 모듈
    compileOnly project(':common:base')

    // utils 모듈 uuid
    implementation project(':utils:util-uuid')

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Test
    testImplementation project(':domain:domain-pos')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation testFixtures(project(':domain:domain-pos'))
    testImplementation project(':common:base')
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
}


// ### QueryDSL 관련 설정 시작 ###
// Querydsl Q Class 생성 위치
def querydslDir = 'build/generated/querydsl'

// java source set 에 Querydsl Q Class 위치 추가
sourceSets {
    main.java.srcDirs += [querydslDir]
}

// 생성된 Q Class가 컴파일될 디렉토리 설정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

// gradle clean 시, Q Class가 위치한 디렉토리 삭제
clean {
    delete file(querydslDir)
}


